<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.3 on Wed Sep 14 19:26:29 2011 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>hiihat_parse_config_file.pro (HiiHAT)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="hiihat_parse_config_file.pro (HiiHAT)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><a id="hiihat_convert_string_typed:source"></a>function hiihat_convert_string_typed, str
<span class="comments">;+</span>
<span class="comments">; helper function to convert numeric strings to float, fix, or string</span>
<span class="comments">; :Params:</span>
<span class="comments">;  str: in, required, type=string</span>
<span class="comments">;   input string to parse</span>
<span class="comments">;-  </span>
  floatval=stregex(str,"^([+-]?[0-9]+\.([0-9]+)?|\.[0-9]+)$", /extract) 
  if floatval then val=float(floatval) else begin
     fixval=stregex(str,"^([+-]?[0-9]+)$", /extract) 
     if fixval then val = fix(fixval) else val = str
  endelse
  return, val
end

<span class="comments">;+</span>
<span class="comments">; Parses configuration parameters in the (global) hiihat.sav file and (user-defined)</span>
<span class="comments">; hiihat.cfg parameter file. If save_defaults is true, the default parameters in hiihat.sav</span>
<span class="comments">; are overwritten. Global parameters can be accessed via the hiihat_get_config_param </span>
<span class="comments">; function.</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   initialization </span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;  save_defaults: in, optional, type=boolean</span>
<span class="comments">;   overwrite defaults saved in hiihat_defaults.sav file</span>
<span class="comments">;  cfg_file: in, optional, type=string</span>
<span class="comments">;   config file to parse (default: hiihat.cfg)</span>
<span class="comments">;  def_file: in, optional, type=string</span>
<span class="comments">;   .sav file to parse and optionally write (default: hiihat_defaults.sav)</span>
<span class="comments">;  defaults_only: in, optional, type=boolean</span>
<span class="comments">;    use default values only (don't parse cfg file)</span>
<span class="comments">;  verbose: in, optional, type=boolean</span>
<span class="comments">;    print verbose output to console</span>
<span class="comments">;</span>
<span class="comments">; :Examples:</span>
<span class="comments">;   The following code will parse the config file "input.cfg" </span>
<span class="comments">;   and save the parsed values to the "defaults.sav" file:</span>
<span class="comments">; </span>
<span class="comments">;   hiihat_parse_config_file, cfg_file='input.cfg', def_file='defaults.sav', $</span>
<span class="comments">;                           save_defaults=1</span>
<span class="comments">;  </span>
<span class="comments">; :History:</span>
<span class="comments">;   Dec 05, 2010 (BDB): Initially commited to svn.</span>
<span class="comments">;</span>
<span class="comments">;-</span>
<a id="hiihat_parse_config_file:source"></a>function hiihat_parse_config_file, cfg_file=cfg_file, def_file=def_file, $
                                   save_defaults=save_defaults, verbose=verbose, $
                                   defaults_only=defaults_only
  compile_opt strictarr

  title='hiihat_parse_config_file'
  
  <span class="comments">;; get the path to this function to get the hiihat path</span>
  func_file_path=routine_filepath(title,/is_function)
  sep = path_sep()
  hiihat_path=strmid(func_file_path,0,strpos(func_file_path,'pro'+sep+'initialization'))

  <span class="comments">;; make sure we have the right one</span>
  if not file_test(hiihat_path+sep+'pro'+sep) then begin
     print, "ERROR: HiiHAT directory not found"
     retall
  endif

  <span class="comments">;; set up default file paths within hiihat directory</span>
  if not keyword_set(cfg_file) then begin
     cfg_file = hiihat_path+'hiihat.cfg'
  endif

  if not keyword_set(def_file) then begin
     def_file = hiihat_path+'hiihat_defaults.sav'
  endif

  if not keyword_set(save_defaults) then save_defaults=0

  <span class="comments">;; load saved default parameters, if available </span>
  if file_test(def_file) then begin
     restore, def_file <span class="comments">;; parses the old "cfg" struct</span>
  endif else begin 
     cfg = {hiihat_path:hiihat_path, cfg_file:cfg_file, def_file:def_file} 
     save_defaults=1 <span class="comments">;; write a new default file</span>
  endelse

  <span class="comments">;; if config file unavailable, skip to cleanup</span>
  if file_test(cfg_file) and not keyword_set(defaults_only) then begin 
     openr, lun, cfg_file, /get_lun 
  endif else begin
     defaults_only=1
     goto, cleanup
  endelse

  <span class="comments">;; since we may need to override the typedefs in the old struct (which</span>
  <span class="comments">;; idl does not allow), create a new struct here </span>
  <span class="comments">;; FIXME: this will allow one to arbitarily change typedefs of config parms</span>

  record = ''  
  new_cfg = {hiihat_path:hiihat_path, cfg_file:cfg_file, def_file:def_file} 
  while (eof(lun) ne 1) do begin
     readf, lun, record
     record = strtrim(record)
     <span class="comments">;; skip comments</span>
     if strmid(record,0,1) eq ';' then continue 

     <span class="comments">;; parse tag/value pair</span>
     tagval = strsplit(record, '=', /extract)  
     tag = strupcase(strtrim(tagval[0]))

     <span class="comments">;; remove trailing comments and get typed value</span>
     valstr = strtrim((strsplit(tagval[1],";", /extract))[0])         
     typed_value = hiihat_convert_string_typed(valstr)

     <span class="comments">;; append to config parms</span>
     new_cfg = create_struct(new_cfg,tag,typed_value) 
  endwhile
  free_lun, lun

  <span class="comments">;; populate config parms with old defaults not parsed in user .cfg file</span>
  cfg_names = tag_names(cfg)
  for i = 0,n_tags(cfg)-1 do begin
     cfg_tag = cfg_names[i]
     idx = where(tag_names(new_cfg) eq cfg_tag)
     if idx eq -1 then begin <span class="comments">;; cfg parm not given in hiihat.cfg file</span>
        new_cfg = create_struct(new_cfg,cfg_tag,cfg.(i))
     endif 
  endfor

  <span class="comments">;; update global config variable accordingly</span>
  cfg = new_cfg

  <span class="comments">;; if the paths are incorrect (e.g. from an old save file), </span>
  <span class="comments">;; update them before writing/returning</span>
  if cfg.HIIHAT_PATH ne hiihat_path then begin     
     cfg.HIIHAT_PATH=hiihat_path
     cfg.CFG_FILE=cfg_file
     cfg.DEF_FILE=def_file
  endif

cleanup:
  <span class="comments">;; dump the newly parsed defaults if desired </span>
  if save_defaults then begin
     write_status = hiihat_check_file(def_file,write=1)
     if write_status eq 0 then begin 
        dialog='Cannot write defaults file: '+def_file
        ok=dialog_message(dialog,title=title)
     endif else begin
        save, filename = def_file, cfg
     endelse
  endif

  debug = cfg.DEBUG
  if not keyword_set(verbose) then verbose = cfg.VERBOSE 
  if debug then begin
     print, "Entering "+title 
     if keyword_set(defaults_only) then begin
        if verbose then begin
           print, 'Config file not found or defaults_only=1'
           print, "Loading defaults from: "+def_file
        endif
     endif else begin
        if verbose then begin 
           print, "Loading defaults from: "+def_file
           print, "Loading configuration from: "+cfg_file
        endif
     endelse

     if save_defaults and verbose then $
        print, "Saving defaults to: "+def_file

     if verbose then help, cfg, /struct
     print, 'Exiting '+title
  endif

  return, cfg
end


</code>
    </div>
  </body>
</html>